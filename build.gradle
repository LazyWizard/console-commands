import groovy.json.JsonSlurper

import java.nio.file.Paths

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.7
defaultTasks 'buildMod'

ext {
    Properties localProps = new Properties()
    localProps.load(new FileInputStream(file('local.properties')))

    // Local build environment config, set in local.properties
    gameDir = localProps.getProperty('starsectorInstallDir')
    lazylibDir = localProps.getProperty('lazylibDir')
    outputPath = localProps.getProperty('outputPath')

    if (project.hasProperty('buildNumber')) ext.modVersion = 'dev (build ' + buildNumber + ")"
    else modVersion = new JsonSlurper().parseText(file('src/main/mod/mod_info.json').text).version

    // JDK config, unnecessary if your JAVA_HOME environment variable is set
    // Need to test this on a new install; something seems broken with latest Kotlin compiler
    //compileJava.options.fork = true
    //compileJava.options.forkOptions.executable = Paths.get(localProps['jdkLocation'].toString(), 'bin', 'javac.exe')
    //compileKotlin.kotlinOptions.jdkHome = Paths.get(localProps['jdkLocation'].toString())
    //gradle.java.home = compileKotlin.kotlinOptions.jdkHome

    // Use Starsector's bundled JRE for bootstrap classpath (ensures compiling with newer JDKs won't cause issues)
    /*tasks.withType(JavaCompile) {
        options.bootstrapClasspath = files(gameDir + '/jre/lib/rt.jar')
    }*/
}

buildscript {
    ext.kotlin_version = '1.3.30'
    ext.dokka_version = '0.9.18'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

repositories {
    mavenCentral()
}

//  Please note: any dependencies marked 'compile' or 'runtime' will be included in the jar!
dependencies {
    compileOnly('org.jetbrains.kotlin:kotlin-stdlib') { transitive = false }
    compileOnly('org.jetbrains.kotlin:kotlin-stdlib-jdk7') { transitive = false }
    compileOnly fileTree(dir: gameDir, include: '*.jar', exclude: '*_obf.jar')
    compileOnly files(lazylibDir + '/jars/LazyLib.jar')
    compileOnly files(lazylibDir + '/jars/LazyLib-Kotlin.jar')
    dokkaRuntime files('lib/dokka-fatjar-0.9.18.jar')
    //compileOnly fileTree(dir: lazylibDir + '/jars/', include: '*.jar')
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6" // At the moment Kotlin only supports Java 6 or 8, and since Starsector uses Java 7...
        noReflect = true
    }
}

jar {
    //from sourceSets.main.java
    from sourceSets.main.kotlin
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } } // Include source files in jar
    destinationDir file(Paths.get(outputPath, 'jars/'))
    archiveName 'lw_Console.jar'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    // Packages excluded from javadoc
    for (def subpackage : ['commands', 'rulecmd', 'ui', 'testing', 'ConsoleOverlay', 'ShowSettings']) {
        packageOptions {
            prefix = "org.lazywizard.console.$subpackage"
            suppress = true
        }
    }
}

task printProps() {
    doLast {
        println 'Game directory: ' + gameDir
        println 'LazyLib directory: ' + lazylibDir
        println 'Output path: ' + outputPath
        println 'Mod version: ' + modVersion
    }
}

task copyModFiles(type: Copy) {
    from 'src/main/mod'
    into outputPath
}

task buildMod(dependsOn: [jar, copyModFiles]) {
    doLast {
        println "Mod folder created at \"${file(outputPath).absolutePath}\""
    }
}

task zipDoc(type: Zip, dependsOn: dokka) {
    from("$buildDir/javadoc")
    include '**/*'
    archiveName = 'javadoc.zip'
    destinationDir = file(outputPath)
}

task zipExample(type: Zip) {
    from('src/main/examplemod')
    into 'Example Mod-Added Commands'
    include '**/*'
    archiveName = 'Example Mod-Added Commands.zip'
    destinationDir = file(outputPath)
}

task zipMod(type: Zip, dependsOn: [clean, buildMod, zipDoc, zipExample]) {
    from(outputPath) { into 'Console Commands' }
    include '**/*'
    archiveName = "Console Commands ${modVersion}.zip"
    destinationDir = file('build/zip/')
    doLast {
        println 'Mod zip placed in \"' + destinationDir.absolutePath + '\"'
    }
}

buildMod.mustRunAfter(clean)
buildMod.mustRunAfter(zipDoc)
buildMod.mustRunAfter(zipExample)

copyModFiles {
    description = 'Copies non-compiled mod files to the output directory.'
    group = 'build'
}

buildMod {
    description = 'Builds the jar and assembles the mod folder.'
    group = 'build'
}

zipExample {
    description = 'Zips the example mod and adds its archive to the mod folder.'
    group = 'build'
}

zipMod {
    description = 'Builds the mod and creates a zip archive ready for distribution.'
    group = 'build'
}
