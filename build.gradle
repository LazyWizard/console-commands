import java.nio.file.Paths
import groovy.json.JsonSlurper

// TODO: Add Example Commands mod as a sub-project and include its zip in the buildMod task
// TODO: Include javadoc.zip in full build (probably only zipMod as doc takes time to build)

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.7
defaultTasks 'buildMod'

ext {
    Properties localProps = new Properties()
    localProps.load(new FileInputStream(file('local.properties')))
    compileJava.options.fork = true
    compileJava.options.forkOptions.executable = Paths.get(localProps['jdkLocation'].toString(), 'bin', 'javac.exe')
    compileKotlin.kotlinOptions.jdkHome = Paths.get(localProps['jdkLocation'].toString())
    ext.gameDir = localProps['starsectorInstallDir']
    ext.lazylib = localProps['lazylibJar']
    ext.outputPath = localProps['outputPath']
    ext.modVersion = new JsonSlurper().parseText(file('src/main/mod/mod_info.json').text).version
}

buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}

//  Please note: any dependencies marked 'compile' or 'runtime' will be included in the jar!
dependencies {
    compile('org.jetbrains.kotlin:kotlin-stdlib') { transitive = false }
    compile('org.jetbrains.kotlin:kotlin-stdlib-jdk7') { transitive = false }
    compileOnly fileTree(dir: gameDir, include: '*.jar', exclude: '*_obf.jar')
    compileOnly files(lazylib)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

jar {
    //from sourceSets.main.java
    from sourceSets.main.kotlin
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    destinationDir file(Paths.get(outputPath, 'jars/'))
    archiveName 'lw_Console.jar'
}

task printProps() {
    doLast {
        println 'Game directory: ' + gameDir
        println 'LazyLib jar: ' + lazylib
        println 'Output path: ' + outputPath
        println 'Mod version: ' + modVersion
    }
}

task copyModFiles(type: Copy) {
    from 'src/main/mod'
    into outputPath
}

task buildMod(dependsOn: [jar, copyModFiles]) {
    doLast {
        println 'Mod folder created at \"' + file(outputPath).absolutePath + '\"'
    }
}

task zipMod(type: Zip, dependsOn: buildMod) {
    from(outputPath) { into 'Console Commands' }
    include '**/*'
    archiveName = "Console Commands ${modVersion}.zip"
    destinationDir = file('build/zip/')
    doLast {
        println 'Mod zip placed in \"' + destinationDir.absolutePath + '\"'
    }
}

copyModFiles {
    description = 'Copies non-compiled mod files to the output directory.'
    group = 'build'
}

buildMod {
    description = 'Builds the jar and assembles the mod folder.'
    group = 'build'
}

zipMod {
    description = 'Builds the mod and creates a zip archive ready for distribution.'
    group = 'build'
}
